if (isRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply from: "../biz-library.gradle"

android{

    signingConfigs {
        debug {
            keyAlias 'ldjc'
            keyPassword 'zdyb_ldjc'
            storeFile file('./jks/zdyb_ldjc.jks')
            storePassword 'zdyb_ldjc'
            //v2SigningEnabled false
        }
        release {
            keyAlias 'ldjc'
            keyPassword 'zdyb_ldjc'
            storeFile file('./jks/zdyb_ldjc.jks')
            storePassword 'zdyb_ldjc'
            //v2SigningEnabled false
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}_${variant.versionName}_${releaseTime()}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    dexOptions {
        //最大堆内存
        javaMaxHeapSize "4g"
        //是否支持大工程模式
        jumboMode = true
        //预编译
        preDexLibraries = true
        //线程数
        threadCount = 8
    }

    namespace rootProject.ext.namespaces.module_diagnosis
    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ['libs']
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
    ndkVersion '21.4.7075529'
}

def releaseTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("Asia/Shanghai"))
}

dependencies {
    implementation project(':lib_common')
    //implementation project(':guiNativelib')

    implementation 'org.apache.commons:commons-compress:1.20'
    implementation 'org.tukaani:xz:1.8'
    implementation 'org.apache.commons:commons-collections4:4.1'

    //implementation files('libs\\app-release.aar')
}